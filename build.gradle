plugins {
	id 'java'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.0.0'
}

apply plugin: 'eclipse'

group="org.asf.software"
version="1.0.0.A1"
def authorname = "AerialWorks Software Foundation"

sourceCompatibility = '1.9'
targetCompatibility = '1.9'

repositories {
	mavenCentral()
	maven { url = "https://aerialworks.ddns.net/maven"; name = "AerialWorks" }
}

dependencies {
	testImplementation 'junit:junit:4.12'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.0'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.0'

	implementation group: 'org.asf.cyan', name: 'CyanUtil', version: '1.0.0.A14'
}

jar {
	manifest {
		attributes([
			"Specification-Title": project.name,
			"Specification-Vendor": "${authorname}",
			"Specification-Version": project.version,
			"Implementation-Title": project.name,
			"Implementation-Version": project.version,
			"Implementation-Vendor" :"${authorname}",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			"Main-Class": "${project.group}.MainWindow"
		])
	}
	exclude "**/extra/**"
	exclude "**/tests/**"
	includeEmptyDirs = false
}

task javaDocs(type: Javadoc) {
	classpath = sourceSets.main.runtimeClasspath
	source = sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: 'javaDocs') {
	from javadoc
	classifier = 'javadoc'
	exclude "**/extra/**"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
	exclude "**/extra/**"
}

artifacts {
	archives shadowJar
	archives sourcesJar
	archives javadocJar
}

test {
	include "**/**Test**"
	exclude "**/**SpecialTest**"
	minHeapSize = "128m"
	maxHeapSize = "2G"
}

task specialTest (type: Test, dependsOn: test) {
	include "**/**SpecialTest**"
	minHeapSize = "128m"
	maxHeapSize = "2G"
}
